#!/bin/csh -fx
### set env variables
setenv CESM2_LE_TOOLS_ROOT $HOME/CESM_CASE_MANAGEMENT_TOOLS
setenv POSTPROCESS_PATH /home/jedwards/workflow/CESM_postprocessing
source $POSTPROCESS_PATH/cesm-env2/bin/activate
#if ($USER==jedwards)
#set test=test
#else
set test=
#endif

#Start years:  1231, 1251, 1281, 1301

setenv CESMROOT /mnt/lustre/share/CESM/cesm2.1.4-rc.06

set COMPSET = BSSP370smbb
set MACHINE = aleph
set RESOLN = f09_g17
set RESUBMIT = 1
set STOP_N=10
set STOP_OPTION=nyears

set rootmbr =  11

set smbr =  11
set embr =  20

@ mb = $smbr
@ me = $embr

# years:  1231, 1251, 1281, 1301

foreach mbr ( `seq $mb $me` )
@ USE_REFDATE = 1231 


if    ($mbr < 10) then
	setenv CASENAME b.e21${test}.${COMPSET}.${RESOLN}.LE2-${USE_REFDATE}.00${mbr}
        set USE_REFCASE=b.e21.BHISTsmbb.f09_g17.LE2-${USE_REFDATE}.00${mbr}
else if ($mbr >= 10 && $mbr < 100) then
	setenv CASENAME b.e21${test}.${COMPSET}.${RESOLN}.LE2-${USE_REFDATE}.0${mbr}
        set USE_REFCASE=b.e21.BHISTsmbb.f09_g17.LE2-${USE_REFDATE}.0${mbr}
else
	setenv CASENAME b.e21${test}.${COMPSET}.${RESOLN}.LE2-${USE_REFDATE}.${mbr}
        set USE_REFCASE=b.e21.BHISTsmbb.f09_g17.LE2-${USE_REFDATE}.${mbr}
endif
setenv CASEROOT /mnt/lustre/share/CESM/${test}/$CASENAME

# ===================================================================================
# specify master executable
# ===================================================================================
if    ($rootmbr < 10) then
	set    ROOTNAME=b.e21${test}.${COMPSET}.${RESOLN}.LE2-${USE_REFDATE}.00${rootmbr}
else if ($rootmbr >= 10 && $rootmbr < 100) then
	set    ROOTNAME=b.e21${test}.${COMPSET}.${RESOLN}.LE2-${USE_REFDATE}.0${rootmbr}
else
	set    ROOTNAME=b.e21${test}.${COMPSET}.${RESOLN}.LE2-${USE_REFDATE}.${rootmbr}
endif

setenv masterroot /mnt/lustre/share/CESM/${test}/$ROOTNAME


echo $CASEROOT
echo $masterroot

# ===================================================================================
# ===================================================================================
#  NOTE:  First member of ensemble creates executable
# ===================================================================================
# ===================================================================================

if ( $mbr == $rootmbr ) then
  $CESMROOT/cime/scripts/create_newcase --case $CASEROOT --res $RESOLN  --mach $MACHINE --compset $COMPSET --output-root /mnt/lustre/share/CESM/output/
  cd $CASEROOT
  
  ./case.setup
  mv user_nl_ww   user_nl_ww.`date +%m%d-%H%M`
  mv user_nl_cam  user_nl_cam.`date +%m%d-%H%M`
  mv user_nl_clm  user_nl_clm.`date +%m%d-%H%M`
  mv user_nl_cpl  user_nl_cpl.`date +%m%d-%H%M`
  mv user_nl_cice user_nl_cice.`date +%m%d-%H%M`
  mv user_nl_mosart user_nl_mosart.`date +%m%d-%H%M`
  cp $CESM2_LE_TOOLS_ROOT/user_nl_files/BSSP370smbb/mbrs1-10/*  $CASEROOT/
  cp $CESM2_LE_TOOLS_ROOT/SourceMods/src.pop/BHISTsmbb/* $CASEROOT/SourceMods/src.pop/
  cp $CESM2_LE_TOOLS_ROOT/SourceMods/src.cam/* $CASEROOT/SourceMods/src.cam/
  cp $CESM2_LE_TOOLS_ROOT/SourceMods/src.clm/* $CASEROOT/SourceMods/src.clm/

  cp     /mnt/lustre/share/CESM/archive/${USE_REFCASE}/rest/2015-01-01-00000/rpointer* /mnt/lustre/share/CESM/output/${CASENAME}/run/
  ln -s  /mnt/lustre/share/CESM/archive/${USE_REFCASE}/rest/2015-01-01-00000/b.e21.BHISTsmbb.* /mnt/lustre/share/CESM/output/${CASENAME}/run/.
 
  ./xmlchange RUN_REFCASE=${USE_REFCASE}
  ./xmlchange RUN_REFDATE=2015-01-01
  ./xmlchange STOP_N=$STOP_N
  ./xmlchange STOP_OPTION=$STOP_OPTION
  ./xmlchange RESUBMIT=$RESUBMIT
  ./xmlchange DOUT_S_ROOT=/mnt/lustre/share/CESM/archive/$CASENAME
  ./xmlchange GET_REFCASE=FALSE

  ./preview_namelists
# qcmd -- ./case.build >& bld.`date +%m%d-%H%M`
  $POSTPROCESS_PATH/cesm-env2/bin/create_postprocess --caseroot $CASEROOT 

# ===================================================================================
# ===================================================================================
# NOTE:  Ensemble members 2-10:
#        (1) are cloned from the first member, 
#        (2) use the executable and SourceMods from the first member.
# ===================================================================================
# ===================================================================================
else

  $CESMROOT/cime/scripts/create_clone --case $CASEROOT --clone $masterroot --keepexe
  cd $CASEROOT
  ./xmlchange DOUT_S_ROOT=/mnt/lustre/share/CESM/archive/$CASENAME
  rm -fr postprocess
  $POSTPROCESS_PATH/cesm-env2/bin/create_postprocess --caseroot $CASEROOT 

  cp     /mnt/lustre/share/CESM/archive/${USE_REFCASE}/rest/2015-01-01-00000/rpointer* /mnt/lustre/share/CESM/output/${CASENAME}/run/
  ln -s  /mnt/lustre/share/CESM/archive/${USE_REFCASE}/rest/2015-01-01-00000/b.e21.BHISTsmbb.* /mnt/lustre/share/CESM/output/${CASENAME}/run/.

  ./xmlchange RUN_REFCASE=${USE_REFCASE}
  ./xmlchange GET_REFCASE=FALSE

  ./preview_namelists
endif

end             # member loop

exit

