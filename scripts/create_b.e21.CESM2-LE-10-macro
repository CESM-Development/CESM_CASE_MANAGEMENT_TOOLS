#!/bin/csh -fx
### set env variables
setenv CESM2_LE_TOOLS_ROOT $HOME/CESM_CASE_MANAGEMENT_TOOLS
setenv POSTPROCESS_PATH /home/jedwards/workflow/CESM_postprocessing
source $POSTPROCESS_PATH/cesm-env2/bin/activate

setenv CESMROOT /mnt/lustre/share/CESM/cesm2.1.2

set COMPSET = BHISTcmip6
set MACHINE = aleph
set RESOLN = f09_g17

set smbr =  1
set embr =  10

@ mb = $smbr
@ me = $embr

foreach mbr ( `seq $mb $me` )
@ USE_REFDATE = 1001 + ( $mbr - 1 ) * 10

if    ($mbr < 10) then
	setenv CASENAME b.e21.${COMPSET}.${RESOLN}.LE2-${USE_REFDATE}.00${mbr}
else if ($mbr >= 10 && $mbr < 100) then
	setenv CASENAME b.e21.${COMPSET}.${RESOLN}.LE2-${USE_REFDATE}.0${mbr}
else
	setenv CASENAME b.e21.${COMPSET}.${RESOLN}.LE2-${USE_REFDATE}.${mbr}
endif
setenv CASEROOT /mnt/lustre/share/CESM/cases/$CASENAME


echo $CASEROOT
if ( $mbr == $smbr ) then
  set masterroot = $CASEROOT
  $CESMROOT/cime/scripts/create_newcase --case $CASEROOT --res $RESOLN  --mach $MACHINE --compset $COMPSET --workflow=timeseries --output-root /mnt/lustre/share/CESM/output/
  cd $CASEROOT
  
  ./case.setup
  mv user_nl_cam user_nl_cam.`date +%m%d-%H%M`
  mv user_nl_clm user_nl_clm.`date +%m%d-%H%M`
  mv user_nl_cpl user_nl_cpl.`date +%m%d-%H%M`
  mv user_nl_cice user_nl_cice.`date +%m%d-%H%M`
  cp $CESM2_LE_TOOLS_ROOT/user_nl_files/* $CASEROOT/
  mv SourceMods SourceMods.`date +%m%d-%H%M`
  cp -r $CESM2_LE_TOOLS_ROOT/SourceMods $CASEROOT/
 
  ./xmlchange RUN_REFCASE=b.e21.B1850.f09_g17.CMIP6-piControl.001
  ./xmlchange RUN_REFDATE=${USE_REFDATE}-01-01
  ./xmlchange DOUT_S_ROOT=/mnt/lustre/share/CESM/archive
  ./xmlchange STOP_N=10
  ./xmlchange STOP_OPTION=nyears
  ./xmlchange RESUBMIT=15
  ./preview_namelists

  ./case.build >& bld.`date +%m%d-%H%M`
 $POSTPROCESS_PATH/cesm-env2/bin/create_postprocess --caseroot $CASEROOT 
else
  $CESMROOT/cime/scripts/create_clone --case $CASEROOT --clone $masterroot --keepexe
  cd $CASEROOT
  ./xmlchange RUN_REFDATE=${USE_REFDATE}-01-01
  ./preview_namelists
endif

end             # member loop

exit

